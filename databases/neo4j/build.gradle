import org.apache.tools.ant.filters.ReplaceTokens

ext {
    server = [ 
        //http or https : Defines which configuration files will be copied in the resulting container
        protocol: "http",
        name: "neo4j.alpha.noti.watch",
        realm: "noti.watch",
        port: "6660",
        numberOfWorker: "10"
    ]
    credentials = [
        [
            username: "neo4j",
            password: "MXwma7vNxgLw"
        ]
    ]
    container = [
        user: "cyr62110", //Your username used to publish container on the Docker Hub.
        name: "notidroid-databases-neo4j",
        version: "1.0"
    ]
    privateRepository = [
        user: "registry",
        password: "wL6cHmQQXmE4",
        host:"registry.noti.watch:5000"
    ]

    neo4jConfTemplateDir = "conf/neo4j"
    nginxConfTemplateDir = "conf/nginx"
}

/**
 * Copy the Dockerfile that will be used to build our image.
 */
task copyDockerfile(type: Copy) {
    def tokens = [
        PORT: project.ext.server.port,
        PROTOCOL: project.ext.server.protocol,
    ]
    from(".") {
        include 'Dockerfile'
        filter(ReplaceTokens, tokens: tokens)
    }
    into buildDir
}

task copyEntrypointScript(type: Copy) {
    from(".") {
        include 'entrypoint.sh'
    }
    into buildDir
}

task copyNeo4JConfiguration(type: Copy) {
    def tokens = [
        PORT: project.ext.server.port,
    ]
    from(neo4jConfTemplateDir) {
        filter(ReplaceTokens, tokens: tokens)
    }
    into "$buildDir/conf/neo4j"
}

task copyNginxConfiguration(dependsOn: ['copyNginxTemplatedConfigurationFiles', 'createNginxBasicAuthUserFile'])

task copyNginxTemplatedConfigurationFiles(type: Copy) {
    def tokens = [
        NAME: project.ext.server.name,
        REALM: project.ext.server.realm,
        PORT: project.ext.server.port,
        WORKER: project.ext.server.numberOfWorker,
    ]
    from(nginxConfTemplateDir) {
        include 'nginx.conf'
        include server.protocol + '.conf'
        filter(ReplaceTokens, tokens: tokens)
    }
    into "$buildDir/conf/nginx"
}

task createNginxBasicAuthUserFile << {
    def userFile = file("$buildDir/conf/nginx/user_file")
    userFile.text = ""
    for(credentials in project.ext.credentials) {
        userFile.append "${credentials.username}:{PLAIN}${credentials.password}\n"
    }
}

task build(type: Exec, dependsOn: [':base/java7:build', 'copyDockerfile', 'copyEntrypointScript', 'copyNeo4JConfiguration', 'copyNginxConfiguration']) {
    workingDir buildDir
    commandLine 'docker', 'build', "-t", "$container.user/$container.name", "."
}

task tagRelease(type: Exec, dependsOn:['build']) {
    commandLine 'docker', 'tag', '-f', "$container.user/$container.name", "$container.user/$container.name:$container.version"
}

task runLocally(type: Exec, dependsOn: 'build') {
    commandLine 'docker', 'run', '-d', "$container.user/$container.name", "/bin/bash"
}

task pushContainer(type: Exec, dependsOn: 'tagRelease') {
    commandLine 'docker', 'push', "$container.user/$container.name:$container.version"
}